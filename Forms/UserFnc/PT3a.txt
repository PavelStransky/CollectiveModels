%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Calculates eigenvalues and creates graph for a system used for studying quantum phase transitions %%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
numev = _p1;
hbar = _p2;
graph = safevalue(_p3; true);
a = safevalue(_p4; -1);
minb = safevalue(_p5; -0.2);
maxb = safevalue(_p6; 0.2);
diffb = safevalue(_p7; 0.005);
numevcalculated = safevalue(_p8; 5 * numev);
omega0 = safevalue(_p9; 1.0);
evgraph = safevalue(_p10; true);

db = maxb - minb;

printline("Calculating eigenvalues...");

for((b = minb; e = list(); bl = list(); ev = list()); 
b < maxb;
(t = pt3(a; b; omega0; hbar);
 print(string(b; "0.00000") ~ " ");
 computespectrum(t; numevcalculated; false; numev);

 add(e; evalues(t));
 add(bl; b);

 b = b + diffb));

if(graph;
(l = length(bl)[0];
 mine = e[0][0];
 maxe = e[0][$];

 x = intervalv(-3; 3; 200);
 dx = x[$] - x[0];

 xx = (x - x[0]) * db / dx + maxb;

 printline("Creating data for graph...");

 for((i = 0; pv = list(); st = list());
 i < numev;
 (add(pv; pointvector(vector(bl); vector(e[][i])));
  add(st; "");
  i = i + 1);
 (add(st; "lcolor = \"red\"; lwidth = 2;");
  add(st; "lcolor = \"green\"; lwidth = 2;");
  st = toarray(st)));

 printline("Creating animation...");

 for((i = 0; pvall = list());
 i < l;
 (pv1 = pv;
  add(pv1; pointvector(vector(bl[i]; bl[i]); vector(mine; maxe)));

  y = pt3potential(x; a; bl[i]);
  add(pv1; pointvector(xx; y));

  if(evgraph;
  for(j = 0; j < numev;
  (add(pv1; pointvector(vector(maxb + 0.1 * db; maxb + 0.9 * db); vector(e[i][j]; e[i][j])));
  j = j + 1)));

  koef = (maxe - mine) * 20;

  pv1 = toarray(pv1);
  add(pvall; pv1);
  i = i + 1);
  pvall = toarray(pvall));;

 printline("Creating graph...");

 gstyle = "maxy = " ~ maxe ~ "; miny = " ~ mine ~ "; maxx = " ~ (2.0 * db + minb) ~ "; minx = " ~ minb ~ "; lcolor = \"blue\"; lstyle = \"line\"; pstyle = \"none\";";
 show(graph(pvall; ; ; gstyle; ; st); "PT3");

 clear(x; y); 
 clear(l; i; st);
 clear(mine; maxe);
 clear(pv1)
));

clear(t; bl; b; db);
